buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

allprojects {
  ext.config = new ConfigSlurper('jenkins').parse(file("$rootDir/gradle/config/buildConfig.groovy").toURL())
}

sourceCompatibility = 1.8
version = '0.0.1'

configurations {
  deployerJars
  runtime.exclude group: "org.apache.ambari", module: "ambari-server"
}

jar {
  manifest.attributes provider: 'gradle'
  manifest {
    attributes  'Implementation-Title': 'Gradle Quickstart',
            'Implementation-Version': version,
            'Main-Class': 'org.apache.ambari.server.BridgeApplication'
  }
}
group = 'org.apache'

shadowJar {
  baseName = project.name + '-shadow'
  group = 'org.apache'
  classifier = null
  version = '0.0.1'
}

artifacts {
  archives shadowJar
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "http://maven.sequenceiq.com/releases" }

}

dependencies {
  compile ('org.apache.ambari:ambari-server:2.0.0.0-SNAPSHOT') {
    exclude group: 'org.apache.ambari', module: 'ambari-server'
  }
  compile 'com.sequenceiq:consul-api:1.10-down'
  compile 'com.google.inject:guice:4.1.0'
  compile 'org.slf4j:slf4j-api:1.7.12'
  compile "com.google.code.gson:gson:2.3"

  compile "org.apache.httpcomponents:httpcore:4.3.1"
  compile "org.apache.httpcomponents:httpclient:4.3.1"
  compile "org.bouncycastle:bcpkix-jdk15on:1.51"

  testCompile 'junit:junit:4.12'
  deployerJars 'org.springframework.build.aws:org.springframework.build.aws.maven:3.0.0.RELEASE'

}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      repository(url: "s3://maven.sequenceiq.com/releases") {
        authentication(userName: "$System.env.AWS_ACCESS_KEY", passphrase: "$System.env.AWS_SECRET_ACCESS_KEY")
      }
      snapshotRepository(url: "s3://maven.sequenceiq.com/snapshots") {
        authentication(userName: "$System.env.AWS_ACCESS_KEY", passphrase: "$System.env.AWS_SECRET_ACCESS_KEY")
      }
    }
  }
}

test {
  systemProperties 'property': 'value'
}


task wrapper(type: Wrapper) { gradleVersion = "2.3" }
